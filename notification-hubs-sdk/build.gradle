apply plugin: 'com.android.library'
apply plugin: 'maven'

def version = '0.7.0'
def PUBLISH_ARTIFACT_ID = 'notification-hubs-android-sdk'
def groupId = 'com.microsoft.azure'

def BINTRAY_USERNAME = "$System.env.BINTRAY_USERNAME"
def BINTRAY_APIKEY = "$System.env.BINTRAY_APIKEY"

def isCI = project.hasProperty('isCI') ? Boolean.valueOf(isCI) : false
def repoUrl = isCI ? "file://" + "$System.env.BUILD_ARTIFACTSTAGINGDIRECTORY" : 'https://api.bintray.com/maven/microsoftazuremobile/SDK/Notification-Hubs-Android-SDK'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.0'

    androidTestImplementation 'androidx.test:core:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestUtil 'androidx.test:orchestrator:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.0.0'

    testImplementation 'androidx.test:core:1.0.0'
    testImplementation 'androidx.test:runner:1.1.0'
    testImplementation 'androidx.test:rules:1.1.0'
    testImplementation 'androidx.test.ext:junit:1.0.0'
}



task clearJar(type: Delete) {
    delete 'build/release/' + PUBLISH_ARTIFACT_ID + '-' + version + '.jar'
}

// step 1
task makeJar(type: Copy) {
    from('build/intermediates/packaged-classes/release/')
    into('build/release/')
    include('classes.jar')
    rename ('classes.jar', PUBLISH_ARTIFACT_ID + '-' +  version + '.jar')
}

makeJar.dependsOn(clearJar, build)


// step 2
uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = groupId
        pom.artifactId = PUBLISH_ARTIFACT_ID
        pom.version = version
        pom.project {
            packaging 'aar'
            name 'Notification Hub'
            description 'NotificationHub Android SDK'
            url 'https://github.com/Azure/azure-notificationhubs'
        }
        repository (
            url: repoUrl
        ) {
            authentication(
                userName: BINTRAY_USERNAME,
                password: BINTRAY_APIKEY
            )
        }
    }
}


task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
    archives file : file('build/release/' + PUBLISH_ARTIFACT_ID + '-' + version + '.jar')
}